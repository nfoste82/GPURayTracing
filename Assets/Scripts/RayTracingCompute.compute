// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

Texture2D<float4> _CheckerboardTexture;
SamplerState sampler_CheckerboardTexture;

float4 _AmbientLight;
float4 _SkyboxLight;

int _NumberOfPasses;
int _ShadowQuality;
int _NumBounces;
float _ShadowRandomness;
float _FocalDistance;
float _GroundSmoothness;

float _Seed;
float2 _Pixel;

int _NumSpheres;
int _NumLights;

static const float PI = 3.141593f;
static const float MaxRefractionDiff = 4;
static const float MaxReflect = 2;

float rand()
{
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    _Seed += 1.0f;
    return result;
}

float3 Combine(float3 a, float3 b)
{
    return float3(max(a.x, b.x), max(a.y, b.y), max(a.z, b.z));
}

// =======================================================
// Sphere
struct Sphere
{
    float3 position;
    float3 color;
    float3 emission;
    float radius;
    float smoothness;
    float opacity;
    float refraction;
};

StructuredBuffer<Sphere> _Spheres;
StructuredBuffer<Sphere> _Lights;

// =======================================================
// Ray
struct Ray
{
    float3 origin;
    float3 direction;
    float3 color;
    float energy;
};

Ray CreateRay(float3 origin, float3 direction)
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.color = float3(0.0f, 0.0f, 0.0f);
    ray.energy = 1.0f;
    return ray;
}

Ray CreateCameraRay(float2 uv)
{
    // Transform the camera origin to world space
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    
    // Invert the perspective projection of the view-space position
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    
    // Transform the direction from camera to world space and normalize
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);

    return CreateRay(origin, direction);
}
// =======================================================

// =======================================================
// RayHit
struct RayHit
{
    float3 position;
    float3 obj_position;
    float3 normal;
    float3 emission;
    float3 color;
    float obj_radius;
    float distance;
    float smoothness;
    float opacity;
    float distanceThroughOpacity;
    float refraction;
};

RayHit CreateRayHit()
{
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.obj_position = float3(0.0f, 0.0f, 0.0f);
    hit.normal = float3(0.0f, 0.0f, 0.0f);
    hit.emission = float3(0.0f, 0.0f, 0.0f);
    hit.color = float3(0.0f, 0.0f, 0.0f);
    hit.obj_radius = 0.0f;
    hit.distance = 1.#INF;
    hit.smoothness = 0.0f;
    hit.opacity = 1.0f;
    hit.refraction = 1.0f;

    return hit;
}
// =======================================================

void IntersectGroundPlane(Ray ray, inout RayHit bestHit)
{
    // Calculate distance along the ray where the ground plane is intersected
    float t = -ray.origin.y / ray.direction.y;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = float3(0.0f, 1.0f, 0.0f);
        bestHit.color = float3(0.8f, 0.8f, 0.8f);
        bestHit.smoothness = _GroundSmoothness;
        bestHit.emission = float3(0.0f, 0.0f, 0.0f);
        bestHit.obj_position = float3(0.0f, 0.0f, 0.0f);
        bestHit.obj_radius = 0.0f;
        bestHit.opacity = 1.0f;
        bestHit.distanceThroughOpacity = 0.0f;
    }
}

void IntersectSphere(Ray ray, inout RayHit bestHit, Sphere sphere)
{
    // Calculate distance along the ray where the sphere is intersected
    float3 d = sphere.position - ray.origin;
    float p1 = dot(ray.direction, d);
    
    // Ray is pointing away from sphere
    if (p1 < 0)
    {
        return;
    }
    
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    
    // Ray missed the sphere
    if (p2sqr < 0)
    {
        return;
    }
    
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.obj_position = sphere.position;
        bestHit.normal = normalize(bestHit.position - sphere.position);
        bestHit.emission = sphere.emission;
        bestHit.color = sphere.color;
        bestHit.obj_radius = sphere.radius;
        bestHit.distance = t;
        bestHit.smoothness = sphere.smoothness;
        bestHit.opacity = sphere.opacity;
        bestHit.distanceThroughOpacity = 0.0f;
        bestHit.refraction = sphere.refraction;
    }
}

RayHit GetNearestIntersection(Ray ray)
{
	RayHit bestHit = CreateRayHit();
	
	IntersectGroundPlane(ray, bestHit);
	
	int i;
	for (i = 0; i < _NumSpheres; i++)
	{
		IntersectSphere(ray, bestHit, _Spheres[i]);
	}
	
	for (i = 0; i < _NumLights; i++)
	{
		IntersectSphere(ray, bestHit, _Lights[i]);
	}
	
	return bestHit;
}

float3 GetClosestPointOnLineSegment(float3 linePointStart, float3 linePointEnd, float3 testPoint)
{
    float3 lineDiff = linePointEnd - linePointStart;
    float lineSegSqrLength = dot(lineDiff, lineDiff);
 
    float3 lineToPoint = testPoint - linePointStart;
    float dotProduct = dot(lineDiff, lineToPoint);

    float percentageAlongLine = dotProduct / lineSegSqrLength;

    if (percentageAlongLine < 0.0f || percentageAlongLine > 1.0f)
    {
        // Point isn't within the line segment
        return float3(0.0f, 0.0f, 0.0f);
    }

    return linePointStart + (percentageAlongLine * (linePointEnd - linePointStart));
}

float3 Refract(float3 sourceDirection, float sourceRefraction, float targetRefraction, float3 targetNormal)
{
    float refraction = (targetRefraction - sourceRefraction) / MaxRefractionDiff;
    float reflectAmount = refraction * MaxReflect;

    // reflect amount
    // ------------------
    // 0 no refraction
    // -2 (-90 degrees)
    // 2 (90 degrees)
    
    reflectAmount = reflectAmount * dot(targetNormal, sourceDirection);
    return normalize((reflectAmount * targetNormal) + sourceDirection);
}

float3 GetLightHittingPointHardShadow(Ray ray, RayHit hit)
{
    float3 combinedLightColor = float3(0.0f, 0.0f, 0.0f);

    int i;
	for (i = 0; i < _NumLights; i++)
	{
        float3 transparentColor = float3(0.0f, 0.0f, 0.0f);	
	
	    float3 lightTotal = float3(0.0f, 0.0f, 0.0f);
	    float3 lightPos = _Lights[i].position;
		float3 ptToLight = lightPos - hit.position;
		ptToLight = normalize(ptToLight);
		
		float3 sideDir = cross(float3(0.0f, 1.0f, 0.0f), ptToLight) * _Lights[i].radius;
		float3 upDir = cross(sideDir, ptToLight) * _Lights[i].radius;
		
        RayHit bestHit = CreateRayHit();
        
        float3 sideOffset = sideDir * ((rand() * _ShadowRandomness) - (rand() * _ShadowRandomness));
        float3 upOffset = upDir * ((rand() * _ShadowRandomness) - (rand() * _ShadowRandomness));
        float3 offsetPt = lightPos + sideOffset + upOffset;
        float3 ptToOffset = offsetPt - hit.position;
        float distanceToLight = length(ptToOffset);
        ptToOffset = normalize(ptToOffset);
        Ray rayToLight = CreateRay(hit.position + (hit.normal * 0.001f), ptToOffset);  
        
        int j;
        for (j = 0; j < _NumSpheres; j++)
        {	       
            IntersectSphere(rayToLight, bestHit, _Spheres[j]);	    
        }
        
        float3 lightColor = _Lights[i].emission;
        float lightStrength = clamp(1.0f / (log2(max(1.0f, distanceToLight)) * 0.5f), 0, 1);                
        float rayNormalDot = dot(ptToOffset, hit.normal);
        float lightPercentageThatHits = rayNormalDot * lightStrength;
        
        if (bestHit.distance > distanceToLight)
        {    
            lightTotal += lightColor * lightPercentageThatHits;
        }
        else if (bestHit.opacity < 1.0f)
        {
            // Calculate distance through sphere
            float3 halfway = GetClosestPointOnLineSegment(bestHit.position, bestHit.position + (2.0f * bestHit.obj_radius * rayToLight.direction), bestHit.obj_position);
            float3 halfOffset = ((halfway - bestHit.position));                   
            
            bestHit.distanceThroughOpacity = length(halfOffset);

            const float lightFocusAmount = max(0.9f, bestHit.distanceThroughOpacity * 2.0f / bestHit.obj_radius);
        
            transparentColor.xyz += (lightColor * lightPercentageThatHits) * bestHit.color * (1 - bestHit.opacity) * lightFocusAmount;
        }
		
		combinedLightColor = Combine(combinedLightColor, lightTotal);
		combinedLightColor = Combine(combinedLightColor, transparentColor.xyz);
    }
	
	return combinedLightColor;
}

float3 GetLightHittingPoint(Ray ray, RayHit hit)
{
    float3 combinedLightColor = float3(0.0f, 0.0f, 0.0f);

    int i;
	for (i = 0; i < _NumLights; i++)
	{
        float3 transparentColor = float3(0.0f, 0.0f, 0.0f);	
	
	    float3 lightTotal = float3(0.0f, 0.0f, 0.0f);
	    float3 lightPos = _Lights[i].position;
		float3 ptToLight = lightPos - hit.position;
		ptToLight = normalize(ptToLight);
		
		float3 sideDir = cross(float3(0.0f, 1.0f, 0.0f), ptToLight) * _Lights[i].radius;
		float3 upDir = cross(sideDir, ptToLight) * _Lights[i].radius;
		
		int x;
		int y;
		for (x = -_ShadowQuality; x <= _ShadowQuality; x++)
		{
		    for (y = -_ShadowQuality; y <= _ShadowQuality; y++)
		    {
		        RayHit bestHit = CreateRayHit();
		    
		        float offsetAmount = (1.0f / max(1, _ShadowQuality)); 
		        
                float3 sideOffset = sideDir * (x + (offsetAmount + rand() * _ShadowRandomness - rand() * _ShadowRandomness));
                float3 upOffset = upDir * (y + (offsetAmount + rand() * _ShadowRandomness - rand() * _ShadowRandomness));
                float3 offsetPt = lightPos + sideOffset + upOffset;
                float3 ptToOffset = offsetPt - hit.position;
                float distanceToLight = length(ptToOffset);
                ptToOffset = normalize(ptToOffset);
                Ray rayToLight = CreateRay(hit.position + (hit.normal * 0.001f), ptToOffset);  
                
                int j;
                for (j = 0; j < _NumSpheres; j++)
                {	       
                    IntersectSphere(rayToLight, bestHit, _Spheres[j]);	    
                }
                
                float3 lightColor = _Lights[i].emission;
                float lightStrength = clamp(1.0f / (log2(max(1.0f, distanceToLight)) * 0.5f), 0, 1);                
                float rayNormalDot = dot(ptToOffset, hit.normal);
                float lightPercentageThatHits = rayNormalDot * lightStrength;
                
                if (bestHit.distance > distanceToLight)
                {    
                    lightTotal += lightColor * lightPercentageThatHits;
                }
                else if (bestHit.opacity < 1.0f)
                {
                    // Calculate distance through sphere
                    float3 halfway = GetClosestPointOnLineSegment(bestHit.position, bestHit.position + (2.0f * bestHit.obj_radius * rayToLight.direction), bestHit.obj_position);
                    float3 halfOffset = ((halfway - bestHit.position));                   
                    
                    bestHit.distanceThroughOpacity = length(halfOffset);
        
                    const float lightFocusAmount = max(0.9f, bestHit.distanceThroughOpacity * 2.0f / bestHit.obj_radius);
                
                    transparentColor.xyz += (lightColor * lightPercentageThatHits) * bestHit.color * (1 - bestHit.opacity) * lightFocusAmount;
                }
		    }
		}
		
		uint numLightSamples = pow((_ShadowQuality + _ShadowQuality + 1), 2);
		float inverseNumLightSamples = min(1.0f, (1.0f / numLightSamples)); 
		combinedLightColor = Combine(combinedLightColor, lightTotal * inverseNumLightSamples);
		combinedLightColor = Combine(combinedLightColor, transparentColor.xyz * inverseNumLightSamples);
    }
	
	return combinedLightColor;
}

float3 GetSkyboxColor(float3 direction)
{
    float theta = acos(direction.y) / -PI;
    float phi = atan2(direction.x, -direction.z) / -PI * 0.5f;
    return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
}

float3 GetTextureColorOnSphere(float3 position, float3 center, float radius)
{
    float3 localPos = position - center;
    
    float v = acos(localPos.z / radius) / PI;
    float u = acos(localPos.x / (radius * sin(PI * v))) / 2.0f * radius;
    
    return _CheckerboardTexture.SampleLevel(sampler_CheckerboardTexture, float2(u, v), 0).xyz;
}

float3 ModifyNormalByBumpColor(float3 normal, float3 bumpColor, float bumpiness)
{
    return (normal + bumpColor * bumpiness) * (1 - bumpiness * 0.5f);
}

float3 GetRandomizedNormalBasedOnAmount(float3 normal, float amount)
{
    float3 normalWithRand = normalize( float3(
                normal.x + rand() * (1 - amount) - rand() * (1 - amount), 
                normal.y + rand() * (1 - amount) - rand() * (1 - amount), 
                normal.z + rand() * (1 - amount) - rand() * (1 - amount)));
    return normalWithRand;
}

float3 GetColorAtHit(Ray ray, RayHit hit)
{
    // Shaders don't support recursion, so we'll do manual passes for each bounce we want to support
    // This function will be terrible for performance for a GPU, but even so the framerate should be decent (unless other settings are cranked up).
    
    if (hit.distance >= 1.#INF)
    {
        return GetSkyboxColor(ray.direction) * _SkyboxLight.xyz;
    }
    
    if ((hit.emission.x + hit.emission.y + hit.emission.z) > 0.0f)
    {
        return hit.emission;
    }
    
    // Randomize the hit normal somewhat based on roughness of the surface
    hit.normal = GetRandomizedNormalBasedOnAmount(hit.normal, hit.smoothness);    
    
    // Second bounce
    Ray secondRay = CreateRay(hit.position + (hit.normal * 0.001f), reflect(ray.direction, hit.normal));
    
    Ray secondRaySplit = CreateRay(hit.position + (hit.normal * 0.001f), reflect(ray.direction, hit.normal));
    secondRaySplit.energy = 0.0f;
    RayHit secondHitSplit = GetNearestIntersection(secondRaySplit);
    
    // In a ray tracer that allowed recursion we would split the ray off here into two rays that would
    // both contribute to the same pixel on the screen. Unfortunately a realistic solution to that requires 
    // recursion (tree of ray results), and compute shaders don't support recursion. Without that
    // we're stuck with transparency that doesn't look just right. A transparent object behind another will
    // look too opaque. Shadows from transparent objects don't look right when looking at them through
    // another transparent object.
    if (hit.opacity < 1.0f)
    {     
        // Get refraction direction after entering sphere
        secondRay.direction = Refract(ray.direction, 1.0f, hit.refraction, hit.normal);
        
        // Calculate exit point/normal for sphere
        float3 halfway = GetClosestPointOnLineSegment(secondRay.origin, secondRay.origin + (2.0f * hit.obj_radius * secondRay.direction), hit.obj_position);
        float3 offset = ((halfway - secondRay.origin) * 2.0f);
        secondRay.origin += offset;
        float3 exitPointNormal = normalize(secondRay.origin - hit.obj_position);
        
        hit.distanceThroughOpacity = length(offset);

        // Now refract again since we've left the sphere
        secondRay.direction = Refract(secondRay.direction, hit.refraction, 1.0f, exitPointNormal);   
        
        // Ray that reflects off of transparent object loses some energy
        secondRaySplit.energy = hit.opacity;
        
        // Ray that goes through transparent object loses some energy
        secondRay.energy = 1.0f - hit.opacity;             
    }
    
    if (secondHitSplit.distance >= 1.#INF)
    {
        secondRaySplit.color = GetSkyboxColor(secondRaySplit.direction) * _SkyboxLight.xyz;
    }
    else if ((secondHitSplit.emission.x + secondHitSplit.emission.y + secondHitSplit.emission.z) > 0.0f)
    {
        secondRaySplit.color = secondHitSplit.emission;
    }
    else
    {
        secondRaySplit.color = GetLightHittingPoint(secondRaySplit, secondHitSplit) * secondHitSplit.color;
    }
       
    RayHit secondHit = GetNearestIntersection(secondRay);    
    
    // Now we finish the color hitting the point of the first ray, which can factor in the depth of any refraction that occurred
    ray.color = Combine(GetLightHittingPoint(ray, hit) * hit.opacity, secondRaySplit.color * secondRaySplit.energy);         
    
    if (secondHit.distance >= 1.#INF)
    {
        return hit.color * Combine(ray.color, (GetSkyboxColor(secondRay.direction) * _SkyboxLight.xyz * secondRay.energy));
    }
    
    if ((secondHit.emission.x + secondHit.emission.y + secondHit.emission.z) > 0.0f)
    {
        return hit.color * Combine(ray.color, secondHit.emission * secondRay.energy);
    }   
       
    secondHit.normal = GetRandomizedNormalBasedOnAmount(secondHit.normal, secondHit.smoothness);    
    
    // Third bounce        
    Ray thirdRay = CreateRay(secondHit.position + (secondHit.normal * 0.001f), reflect(secondRay.direction, secondHit.normal));
    thirdRay.energy = secondRay.energy;
    if (secondHit.opacity < 1.0f)
    {    
        // Get refraction direction after entering sphere
        thirdRay.direction = Refract(secondRay.direction, 1.0f, secondHit.refraction, secondHit.normal);
        
        // Calculate exit point/normal for sphere
        float3 halfway = GetClosestPointOnLineSegment(thirdRay.origin, thirdRay.origin + (2.0f * secondHit.obj_radius * thirdRay.direction), secondHit.obj_position);
        float3 offset = ((halfway - thirdRay.origin) * 2.0f);
        thirdRay.origin += offset;        
        float3 exitPointNormal = normalize(thirdRay.origin - secondHit.obj_position);

        secondHit.distanceThroughOpacity = length(offset);
        
        // Now refract again since we've left the sphere
        thirdRay.direction = Refract(thirdRay.direction, secondHit.refraction, 1.0f, exitPointNormal);   
    }
    RayHit thirdHit = GetNearestIntersection(thirdRay); 
    
    secondRay.color = GetLightHittingPointHardShadow(secondRay, secondHit) * secondHit.opacity * secondRay.energy;      
    
    if (thirdHit.distance >= 1.#INF)
    {
        float3 secondResult = secondHit.color * Combine(secondRay.color * secondRay.energy, (GetSkyboxColor(thirdRay.direction) * _SkyboxLight.xyz * thirdRay.energy));
        return hit.color * Combine(ray.color, secondResult);
    }
    
    if ((thirdHit.emission.x + thirdHit.emission.y + thirdHit.emission.z) > 0.0f)
    {
        float3 secondResult = secondHit.color * Combine(secondRay.color * secondRay.energy, thirdHit.emission * thirdRay.energy);
        return hit.color * Combine(ray.color, secondResult);
    }
    
    thirdHit.normal = GetRandomizedNormalBasedOnAmount(thirdHit.normal, thirdHit.smoothness);
    
    thirdRay.color = GetLightHittingPointHardShadow(thirdRay, thirdHit) * thirdHit.opacity * GetSkyboxColor(thirdRay.direction) * _SkyboxLight.xyz;   
    
    return hit.color * Combine(ray.color, secondHit.color * Combine(secondRay.color * secondRay.energy, thirdHit.color * thirdRay.color * thirdRay.energy));     
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    _Pixel = id.xy;
    
    // Get the dimensions of the RenderTexture
    uint width, height;
    Result.GetDimensions(width, height);

    // Transform pixel to [-1,1] range
    float2 uv = (id.xy / float2(width, height) * 2.0f - 1.0f) + float2(1/(float)width * rand(), 1/(float)height * rand());
            
    float3 result = float3(0.0f, 0.0f, 0.0f);
    for (int i = 0; i < _NumberOfPasses; i++)
    {
        Ray ray = CreateCameraRay(uv);
        
        // We find the camera focal point, then shift the ray origin, and find the adjusted ray
        // direction after that shift. This gives us depth of field.
        {
            float3 focalPoint = ray.origin + ray.direction * _FocalDistance;
            
            float shiftAmount = 0.005f;
            ray.origin.x += rand() * shiftAmount - rand() * shiftAmount;
            ray.origin.y += rand() * shiftAmount - rand() * shiftAmount;
            ray.origin.z += rand() * shiftAmount - rand() * shiftAmount;
            
            ray.direction = normalize(focalPoint - ray.origin);
        } 
        
        RayHit hit = GetNearestIntersection(ray);        
        result += GetColorAtHit(ray, hit);
    }
   
    Result[id.xy] = float4(result / _NumberOfPasses, 1);
}
